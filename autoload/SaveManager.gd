extends Node

signal save_import_finished(slot: int, ok: bool, message: String)

const LEGACY_SAVE_PATH = "user://save.json"
const SAVE_SLOT_PATH = "user://save_slot_%d.json"
const SAVE_VERSION = 2
const SLOT_COUNT = 3
const MAX_IMPORT_BYTES = 1024 * 1024
const ScriptWorkspace = preload("res://systems/ScriptWorkspace.gd")

var dados: Dictionary = {}
var current_slot: int = 0
var _pending_import_slot := 0
var _web_import_callback = null

func _ready() -> void:
	migrate_legacy_save_if_needed()

func set_current_slot(slot: int) -> void:
	if not _is_valid_slot(slot):
		push_warning("Slot de save invalido: %d" % slot)
		return
	current_slot = slot

func get_current_slot() -> int:
	return current_slot

func get_save_path(slot: int) -> String:
	if not _is_valid_slot(slot):
		push_warning("Slot de save invalido: %d" % slot)
		return ""
	return SAVE_SLOT_PATH % slot

func has_save(slot: int) -> bool:
	var path := get_save_path(slot)
	return not path.is_empty() and FileAccess.file_exists(path)

func delete_save(slot: int) -> void:
	var path := get_save_path(slot)
	if path.is_empty() or not FileAccess.file_exists(path):
		return
	DirAccess.remove_absolute(path)
	if current_slot == slot:
		current_slot = 0
		dados.clear()

func salvar(dinheiro: int = -1, mechanics: Dictionary = {}, upgrades: Array = []) -> void:
	_atualizar_estado_legacy(dinheiro, mechanics, upgrades)
	save_game()

func save_game(slot: int = 0) -> void:
	var target_slot := _resolve_slot(slot)
	if target_slot == 0:
		push_warning("Tentativa de salvar sem slot ativo.")
		return

	var save_data = _montar_save_data()
	save_data["meta"]["last_saved_unix"] = Time.get_unix_time_from_system()
	var path := get_save_path(target_slot)
	var file = FileAccess.open(path, FileAccess.WRITE)
	if file == null:
		push_error("Erro ao salvar o jogo no Slot %d" % target_slot)
		return

	file.store_string(JSON.stringify(save_data, "\t"))
	file.close()
	dados = save_data
	current_slot = target_slot
	print("salvo slot %d" % target_slot)

func carregar() -> Dictionary:
	return load_game()

func load_game(slot: int = 0) -> Dictionary:
	var target_slot := _resolve_slot(slot)
	if target_slot == 0:
		push_warning("Tentativa de carregar sem slot ativo.")
		dados = create_new_save_data()
		_carregar_sistemas(dados)
		return _dados_compatibilidade(dados)

	current_slot = target_slot
	var path := get_save_path(target_slot)
	if not FileAccess.file_exists(path):
		dados = create_new_save_data()
		_carregar_sistemas(dados)
		save_game(target_slot)
		return _dados_compatibilidade(dados)

	var loaded_data := _read_save_file(path)
	if loaded_data.is_empty():
		push_error("Save do Slot %d corrompido ou invalido" % target_slot)
		dados = create_new_save_data()
		_carregar_sistemas(dados)
		return _dados_compatibilidade(dados)

	dados = _validar_dados(loaded_data)
	_carregar_sistemas(dados)
	return _dados_compatibilidade(dados)

func resetar() -> void:
	var target_slot := _resolve_slot(0)
	if target_slot == 0:
		push_warning("Tentativa de criar novo jogo sem slot ativo.")
		return
	dados = create_new_save_data()
	_carregar_sistemas(dados)
	save_game(target_slot)

func create_new_save_data() -> Dictionary:
	return _save_padrao()

func get_slot_info(slot: int) -> Dictionary:
	var info := {
		"slot": slot,
		"exists": false,
		"corrupted": false,
		"money": 0,
		"last_saved_unix": 0,
		"last_saved_text": "",
		"summary": "Slot vazio"
	}
	if not _is_valid_slot(slot):
		info["summary"] = "Slot invalido"
		return info

	var path := get_save_path(slot)
	if not FileAccess.file_exists(path):
		return info

	var loaded_data := _read_save_file(path)
	if loaded_data.is_empty():
		info["exists"] = true
		info["corrupted"] = true
		info["summary"] = "Save corrompido"
		return info

	var valid_data := _validar_dados(loaded_data)
	var game: Dictionary = valid_data.get("game", {})
	var meta: Dictionary = valid_data.get("meta", {})
	var upgrades: Dictionary = valid_data.get("shop", {}).get("comprados", {})
	var last_saved := int(meta.get("last_saved_unix", 0))
	var money := int(game.get("dinheiro", 0))

	info["exists"] = true
	info["money"] = money
	info["last_saved_unix"] = last_saved
	info["last_saved_text"] = _format_timestamp(last_saved)
	info["summary"] = "Dinheiro: R$ %d\nUpgrades: %d" % [money, upgrades.size()]
	if last_saved > 0:
		info["summary"] += "\nUltimo save: " + str(info["last_saved_text"])
	return info

func migrate_legacy_save_if_needed() -> void:
	if not FileAccess.file_exists(LEGACY_SAVE_PATH):
		return
	for slot in range(1, SLOT_COUNT + 1):
		if has_save(slot):
			return

	var loaded_data := _read_save_file(LEGACY_SAVE_PATH)
	if loaded_data.is_empty():
		push_warning("Save antigo encontrado, mas nao foi possivel migrar.")
		return

	var migrated_data := _validar_dados(loaded_data)
	migrated_data["meta"]["last_saved_unix"] = Time.get_unix_time_from_system()
	var file = FileAccess.open(get_save_path(1), FileAccess.WRITE)
	if file == null:
		push_error("Erro ao migrar save antigo para Slot 1")
		return

	file.store_string(JSON.stringify(migrated_data, "\t"))
	file.close()
	print("Save antigo migrado para Slot 1")

func solicitar_save(_motivo := "") -> void:
	salvar()

func export_save(slot: int) -> Dictionary:
	if not _is_valid_slot(slot):
		return {"ok": false, "message": "Slot invalido."}
	if not OS.has_feature("web"):
		return {"ok": false, "message": "Exportacao disponivel apenas no navegador."}
	if not has_save(slot):
		return {"ok": false, "message": "Nao ha save para exportar."}

	var path := get_save_path(slot)
	var file = FileAccess.open(path, FileAccess.READ)
	if file == null:
		return {"ok": false, "message": "Nao foi possivel ler o save."}

	var buffer := file.get_buffer(file.get_length())
	file.close()
	var file_name := "jogo_educativo_slot_%d.json" % slot
	JavaScriptBridge.download_buffer(buffer, file_name, "application/json")
	return {"ok": true, "message": "Download do Slot %d iniciado." % slot}

func import_save_from_browser(slot: int) -> Dictionary:
	if not _is_valid_slot(slot):
		return {"ok": false, "message": "Slot invalido."}
	if not OS.has_feature("web"):
		return {"ok": false, "message": "Importacao disponivel apenas no navegador."}
	if _pending_import_slot != 0:
		return {"ok": false, "message": "Ja existe uma importacao em andamento."}

	_pending_import_slot = slot
	_web_import_callback = JavaScriptBridge.create_callback(_on_web_import_file_loaded)
	var window = JavaScriptBridge.get_interface("window")
	window.__godotImportSaveCallback = _web_import_callback
	JavaScriptBridge.eval("""
(function () {
	const callback = window.__godotImportSaveCallback;
	if (typeof callback !== "function") {
		return;
	}

	const input = document.createElement("input");
	input.type = "file";
	input.accept = "application/json,.json";
	input.style.display = "none";

	const cleanup = function () {
		input.remove();
		delete window.__godotImportSaveCallback;
	};

	input.onchange = function () {
		const file = input.files && input.files[0];
		if (!file) {
			callback("", "cancel");
			cleanup();
			return;
		}
		if (file.size > 1048576) {
			callback("", "too_large");
			cleanup();
			return;
		}

		const reader = new FileReader();
		reader.onload = function () {
			callback(String(reader.result || ""), "ok");
			cleanup();
		};
		reader.onerror = function () {
			callback("", "error");
			cleanup();
		};
		reader.readAsText(file);
	};
	input.oncancel = function () {
		callback("", "cancel");
		cleanup();
	};

	document.body.appendChild(input);
	input.click();
})();
""", true)
	return {"ok": true, "message": "Escolha o arquivo de save do Slot %d." % slot}

func import_save_text(slot: int, texto: String) -> Dictionary:
	if not _is_valid_slot(slot):
		return {"ok": false, "message": "Slot invalido."}
	if texto.strip_edges().is_empty():
		return {"ok": false, "message": "Arquivo de save vazio."}
	if texto.to_utf8_buffer().size() > MAX_IMPORT_BYTES:
		return {"ok": false, "message": "Arquivo de save muito grande."}

	var json := JSON.new()
	var erro := json.parse(texto)
	if erro != OK or not (json.data is Dictionary):
		return {"ok": false, "message": "Arquivo de save invalido."}

	var imported_data: Dictionary = json.data
	if not _looks_like_save_data(imported_data):
		return {"ok": false, "message": "Arquivo nao parece ser um save deste jogo."}

	imported_data = _validar_dados(imported_data)
	imported_data["meta"]["last_saved_unix"] = Time.get_unix_time_from_system()
	var file = FileAccess.open(get_save_path(slot), FileAccess.WRITE)
	if file == null:
		return {"ok": false, "message": "Nao foi possivel gravar o save importado."}

	file.store_string(JSON.stringify(imported_data, "\t"))
	file.close()
	dados = imported_data
	current_slot = slot
	_carregar_sistemas(dados)
	return {"ok": true, "message": "Slot %d importado com sucesso." % slot}

func get_tutorial_data() -> Dictionary:
	if dados.is_empty():
		if current_slot == 0:
			return create_new_save_data().get("tutorial", {}).duplicate(true)
		carregar()
	dados = _validar_dados(dados)
	return dados.get("tutorial", {}).duplicate(true)

func set_tutorial_step(step: int) -> void:
	if dados.is_empty():
		if current_slot == 0:
			return
		carregar()
	dados = _validar_dados(dados)
	dados["tutorial"]["step"] = max(0, step)
	salvar()

func complete_tutorial() -> void:
	if dados.is_empty():
		if current_slot == 0:
			return
		carregar()
	dados = _validar_dados(dados)
	dados["tutorial"]["completed"] = true
	salvar()

func _read_save_file(path: String) -> Dictionary:
	var file = FileAccess.open(path, FileAccess.READ)
	if file == null:
		return {}
	var texto = file.get_as_text()
	file.close()
	var json = JSON.new()
	var erro = json.parse(texto)
	if erro != OK or not (json.data is Dictionary):
		return {}
	return json.data

func _resolve_slot(slot: int) -> int:
	if slot == 0:
		slot = current_slot
	if not _is_valid_slot(slot):
		return 0
	return slot

func _is_valid_slot(slot: int) -> bool:
	return slot >= 1 and slot <= SLOT_COUNT

func _on_web_import_file_loaded(args: Array) -> void:
	var slot := _pending_import_slot
	_pending_import_slot = 0
	_web_import_callback = null

	if args.size() < 2 or str(args[1]) != "ok":
		save_import_finished.emit(slot, false, "Importacao cancelada.")
		return

	var result := import_save_text(slot, str(args[0]))
	save_import_finished.emit(slot, bool(result.get("ok", false)), str(result.get("message", "")))

func _looks_like_save_data(data: Dictionary) -> bool:
	for key in ["meta", "game", "shop", "stock", "interpreter", "tutorial"]:
		if data.has(key):
			return true
	for legacy_key in ["dinheiro", "unlocked_mechanics", "upgrades", "script_text"]:
		if data.has(legacy_key):
			return true
	return false

func _format_timestamp(unix_time: int) -> String:
	if unix_time <= 0:
		return ""
	var date := Time.get_datetime_dict_from_unix_time(unix_time)
	return "%02d/%02d/%04d %02d:%02d" % [
		int(date.get("day", 1)),
		int(date.get("month", 1)),
		int(date.get("year", 1970)),
		int(date.get("hour", 0)),
		int(date.get("minute", 0))
	]

func _montar_save_data() -> Dictionary:
	return _validar_dados({
		"meta": {
			"save_version": SAVE_VERSION
		},
		"game": _coletar_dados_sistema(GameManager),
		"shop": _coletar_dados_sistema(UpgradeManager),
		"stock": _coletar_dados_sistema(StockSystem),
		"interpreter": _coletar_dados_sistema(InterpreterSystem),
		"tutorial": _validar_tutorial(dados.get("tutorial", {}))
	})

func _coletar_dados_sistema(system: Object) -> Dictionary:
	if system != null and system.has_method("get_save_data"):
		var save_data = system.get_save_data()
		if save_data is Dictionary:
			return save_data
	return {}

func _carregar_sistemas(save_data: Dictionary) -> void:
	_carregar_sistema(GameManager, save_data.get("game", {}))
	_carregar_sistema(UpgradeManager, save_data.get("shop", {}))
	_carregar_sistema(StockSystem, save_data.get("stock", {}))
	_carregar_sistema(InterpreterSystem, save_data.get("interpreter", {}))

func _carregar_sistema(system: Object, system_data: Variant) -> void:
	if system != null and system.has_method("load_save_data") and system_data is Dictionary:
		system.load_save_data(system_data)

func _atualizar_estado_legacy(dinheiro: int, mechanics: Dictionary, upgrades: Array) -> void:
	if dinheiro >= 0:
		GameManager.money = dinheiro

	if not mechanics.is_empty():
		for key in GameManager.unlocked_mechanics:
			if mechanics.has(key):
				GameManager.unlocked_mechanics[key] = bool(mechanics[key])

	if not upgrades.is_empty():
		GameManager.upgrades = upgrades.duplicate()

func _save_padrao() -> Dictionary:
	var default_workspace := ScriptWorkspace.new().serialize()
	return {
		"meta": {
			"save_version": SAVE_VERSION,
			"last_saved_unix": 0
		},
		"game": {
			"dinheiro": 0,
			"unlocked_mechanics": {
				"sum": true,
				"cart": false,
				"discount": false,
				"change": false,
				"stock": false,
				"if": false,
				"sensor": false
			},
			"upgrades": []
		},
		"shop": {
			"comprados": {}
		},
		"stock": {
			"quantities": {}
		},
		"interpreter": {
			"script_text": "",
			"script_workspace": default_workspace
		},
		"tutorial": {
			"completed": false,
			"step": 0
		}
	}

func _validar_dados(data: Dictionary) -> Dictionary:
	var resultado = _save_padrao()
	var normalizado = _normalizar_formato(data)

	if normalizado.has("meta") and normalizado["meta"] is Dictionary:
		var meta = normalizado["meta"]
		if meta.has("save_version") and (meta["save_version"] is int or meta["save_version"] is float):
			resultado["meta"]["save_version"] = int(meta["save_version"])
		if meta.has("last_saved_unix") and (meta["last_saved_unix"] is int or meta["last_saved_unix"] is float):
			resultado["meta"]["last_saved_unix"] = int(meta["last_saved_unix"])

	if normalizado.has("game") and normalizado["game"] is Dictionary:
		resultado["game"] = _validar_game(normalizado["game"])

	if normalizado.has("shop") and normalizado["shop"] is Dictionary:
		resultado["shop"] = _validar_shop(normalizado["shop"])

	if normalizado.has("stock") and normalizado["stock"] is Dictionary:
		resultado["stock"] = _validar_stock(normalizado["stock"])

	if normalizado.has("interpreter") and normalizado["interpreter"] is Dictionary:
		resultado["interpreter"] = _validar_interpreter(normalizado["interpreter"])

	if normalizado.has("tutorial") and normalizado["tutorial"] is Dictionary:
		resultado["tutorial"] = _validar_tutorial(normalizado["tutorial"])

	return resultado

func _normalizar_formato(data: Dictionary) -> Dictionary:
	if data.has("game") or data.has("meta"):
		return data

	var convertido = _save_padrao()
	convertido["game"]["dinheiro"] = data.get("dinheiro", convertido["game"]["dinheiro"])
	convertido["game"]["unlocked_mechanics"] = data.get("unlocked_mechanics", convertido["game"]["unlocked_mechanics"])
	convertido["game"]["upgrades"] = data.get("upgrades", convertido["game"]["upgrades"])
	if data.has("script_text") and data["script_text"] is String:
		convertido["interpreter"]["script_text"] = data["script_text"]
	return convertido

func _validar_game(data: Dictionary) -> Dictionary:
	var resultado = _save_padrao()["game"]

	if data.has("dinheiro") and (data["dinheiro"] is int or data["dinheiro"] is float):
		resultado["dinheiro"] = int(data["dinheiro"])

	if data.has("unlocked_mechanics") and data["unlocked_mechanics"] is Dictionary:
		for key in resultado["unlocked_mechanics"]:
			if data["unlocked_mechanics"].has(key):
				resultado["unlocked_mechanics"][key] = bool(data["unlocked_mechanics"][key])

	if data.has("upgrades") and data["upgrades"] is Array:
		resultado["upgrades"] = data["upgrades"].duplicate()

	return resultado

func _validar_shop(data: Dictionary) -> Dictionary:
	var resultado = _save_padrao()["shop"]

	if data.has("comprados"):
		if data["comprados"] is Dictionary:
			for id in data["comprados"]:
				resultado["comprados"][str(id)] = bool(data["comprados"][id])
		elif data["comprados"] is Array:
			for id in data["comprados"]:
				resultado["comprados"][str(id)] = true

	return resultado

func _validar_interpreter(data: Dictionary) -> Dictionary:
	var resultado = _save_padrao()["interpreter"]
	var workspace := ScriptWorkspace.new()

	if data.has("script_workspace") and data["script_workspace"] is Dictionary:
		var workspace_data: Dictionary = data["script_workspace"]
		if workspace_data.has("scripts") and workspace_data["scripts"] is Array and not workspace_data["scripts"].is_empty():
			workspace.deserialize(workspace_data)
		elif data.has("script_text") and data["script_text"] is String:
			workspace.migrate_old_save_if_needed(data)
		else:
			workspace.deserialize(workspace_data)
	elif data.has("scripts") and data["scripts"] is Array:
		workspace.deserialize(data)
	elif data.has("script_text") and data["script_text"] is String:
		workspace.migrate_old_save_if_needed(data)
	else:
		workspace.deserialize({})

	resultado["script_workspace"] = workspace.serialize()
	resultado["script_text"] = workspace.get_active_source()

	return resultado

func _validar_stock(data: Dictionary) -> Dictionary:
	var resultado = _save_padrao()["stock"]

	if data.has("quantities") and data["quantities"] is Dictionary:
		for item_name in data["quantities"]:
			var quantity = data["quantities"][item_name]
			if quantity is int or quantity is float:
				resultado["quantities"][str(item_name)] = max(0, int(quantity))

	return resultado

func _validar_tutorial(data: Dictionary) -> Dictionary:
	var resultado = _save_padrao()["tutorial"]

	if data.has("completed"):
		resultado["completed"] = bool(data["completed"])

	if data.has("step") and (data["step"] is int or data["step"] is float):
		resultado["step"] = max(0, int(data["step"]))

	return resultado

func _dados_compatibilidade(save_data: Dictionary) -> Dictionary:
	var game = save_data.get("game", {})
	var compat = save_data.duplicate(true)
	compat["dinheiro"] = game.get("dinheiro", 0)
	compat["unlocked_mechanics"] = game.get("unlocked_mechanics", {})
	compat["upgrades"] = game.get("upgrades", [])
	return compat
